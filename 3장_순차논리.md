# 3장_순차논리
#밑바닥부터만드는컴퓨터시스템/3장_순차논리

시간이 흘러도 값을 유지하고, 특정 값을 다시 불러오는 칩을  **순차논리**를 학습하여  설계한다.

## 설계할 순차 칩 목록.
- DFF
- Register
- Ram
- PC

#### DFF(Data Flip Flop)
순차 논리를 표현가능하게 만들어주는 Chip
**즉 시간이 지나도(tic->toc) 기존의 값을 유지하는 역할**을 한다.
out(t) = in(t-1) 을 수행하며 t-1은 preState , t 는 resState로 표기한다.
여기에서의 t - (t-1) 의 1은 클록 주기를 의미한다.


![](3%E1%84%8C%E1%85%A1%E1%86%BC_%E1%84%89%E1%85%AE%E1%86%AB%E1%84%8E%E1%85%A1%E1%84%82%E1%85%A9%E1%86%AB%E1%84%85%E1%85%B5/9B1EFD5B-2F5E-4A2C-974C-B5E3BCEDEFB6.png)



#### Bit
1비트 레지스터
시간이 지나도 값을 저장하고 로드할 수 있는 장치이다.
Input values : in(새롭게 들어오는 값) , load(값을 유지 or 변경을 결정)
Output value : load bit가 1에 해당하는 값은 변경 , 그외에는 변경하지 않음.

```vhdl
/**
 * 1-bit register:
 * If load[t] == 1 then out[t+1] = in[t]
 *                 else out does not change (out[t+1] = out[t])
 */

CHIP Bit {
    IN in, load;
    OUT out;

    PARTS:
    // Put your code here:
    Mux(a=preState, b=in, sel=load, out=resState);
    DFF(in=resState, out=preState,out=out);
}

```

#### Register
16Bit Register이다. 단순히 Bit.hdl 을 16개 사용한다.
```vhdl
/**
 * 16-bit register:
 * If load[t] == 1 then out[t+1] = in[t]
 * else out does not change
 */

CHIP Register {
    IN in[16], load;
    OUT out[16];

    PARTS:
    // Put your code here:
    Bit(in=in[0], load=load, out=out[0]);
    Bit(in=in[1], load=load, out=out[1]);
    Bit(in=in[2], load=load, out=out[2]);
    Bit(in=in[3], load=load, out=out[3]);
    Bit(in=in[4], load=load, out=out[4]);
    Bit(in=in[5], load=load, out=out[5]);
    Bit(in=in[6], load=load, out=out[6]);
    Bit(in=in[7], load=load, out=out[7]);
    Bit(in=in[8], load=load, out=out[8]);
    Bit(in=in[9], load=load, out=out[9]);
    Bit(in=in[10], load=load, out=out[10]);
    Bit(in=in[11], load=load, out=out[11]);
    Bit(in=in[12], load=load, out=out[12]);
    Bit(in=in[13], load=load, out=out[13]);
    Bit(in=in[14], load=load, out=out[14]);
    Bit(in=in[15], load=load, out=out[15]);
}

```

#### Ram8
16Bit Register 8개를 직접 접근하여 특정 Register 상태를 변경 / 유지할수 있는 장치이다.
**In 은 특정 Registser에 입력될 값**이다. 
Registser의 주소값에 해당 된다. 즉 **address는 Register를 선택**한다.
load는 address로 선택된 **Registser내부의 변경/수정할 Bit를 결정**한다. 

3비트의 Address를 기반으로 **8개중 하나의 Registser를 선택**한다. ( 즉 입력된 **load비트가 전송될 Register 1개를 선택**한다. -> DMux8Way)    

선택된 1개의 Registser의 결과를 out으로 선택한다. ( Ram8에는 총 8개의 Register가 등록되어있으므로 **8개의 입력 중 Address에 따라 1개의 Register를 선택**한다. -> Mux8Way16)

```vhdl
/**
 * Memory of 8 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux8Way(in=load, sel=address, a=load0, b=load1, c=load2, d=load3, e=load4, f=load5, g=load6, h=load7);
    Register(in=in, load=load0, out=out0);// 0
    Register(in=in, load=load1, out=out1);// 1
    Register(in=in, load=load2, out=out2);// 2
    Register(in=in, load=load3, out=out3);// 3
    Register(in=in, load=load4, out=out4);// 4
    Register(in=in, load=load5, out=out5);// 5
    Register(in=in, load=load6, out=out6);// 6
    Register(in=in, load=load7, out=out7);// 7
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, sel=address, out=out);
}

```


### 이후의 Ram은 Address bit의 의미만 정확하게 이해했다면, 굉장히 쉬운 설계이다. 1GB Ram을 설계할때  1bit * 8 * 1024 * .. 이딴식으로 설계하지 않은걸 감사히 여기자.

#### Ram64
```vhdl
/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // Put your code here:

    DMux8Way(in=load, sel=address[3..5], a=load0, b=load1, c=load2, d=load3, e=load4, f=load5, g=load6, h=load7);
    RAM8(in=in, load=load0, address=address[0..2], out=out0);//0
    RAM8(in=in, load=load1, address=address[0..2], out=out1);//1
    RAM8(in=in, load=load2, address=address[0..2], out=out2);//2
    RAM8(in=in, load=load3, address=address[0..2], out=out3);//3
    RAM8(in=in, load=load4, address=address[0..2], out=out4);//4
    RAM8(in=in, load=load5, address=address[0..2], out=out5);//5
    RAM8(in=in, load=load6, address=address[0..2], out=out6);//6
    RAM8(in=in, load=load7, address=address[0..2], out=out7);//7
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, sel=address[3..5], out=out);
}

```

#### PC
-> 아우 어렵다.
감사하자. Sol의 설명이 정말 좋았으므로 토요일 오전에 다시 듣자.ㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋㅋ고맙다형

```vhdl
/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in = preState, out = nextState);
    Mux16(a = preState, b = nextState, sel = inc, out = out0);
    Mux16(a = out0, b = in, sel = load, out = out1);
    Mux16(a = out1, b = false, sel = reset, out = out2);
    Register(in = out2, load = true, out = out, out = preState);
}

```


#### Ram512
```vhdl
/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    // Put your code here:

    DMux8Way(in=load, sel=address[6..8], a=load0, b=load1, c=load2, d=load3, e=load4, f=load5, g=load6, h=load7);
    RAM64(in=in, load=load0, address=address[0..5], out=out0);//0
    RAM64(in=in, load=load1, address=address[0..5], out=out1);//1
    RAM64(in=in, load=load2, address=address[0..5], out=out2);//2
    RAM64(in=in, load=load3, address=address[0..5], out=out3);//3
    RAM64(in=in, load=load4, address=address[0..5], out=out4);//4
    RAM64(in=in, load=load5, address=address[0..5], out=out5);//5
    RAM64(in=in, load=load6, address=address[0..5], out=out6);//6
    RAM64(in=in, load=load7, address=address[0..5], out=out7);//7
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, sel=address[6..8], out=out);
}

```

#### Ram4K
```vhdl
/**
 * Memory of 4K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    PARTS:
    // Put your code here:

    DMux8Way(in=load, sel=address[9..11], a=load0, b=load1, c=load2, d=load3, e=load4, f=load5, g=load6, h=load7);
    RAM512(in=in, load=load0, address=address[0..8], out=out0);//0
    RAM512(in=in, load=load1, address=address[0..8], out=out1);//1
    RAM512(in=in, load=load2, address=address[0..8], out=out2);//2
    RAM512(in=in, load=load3, address=address[0..8], out=out3);//3
    RAM512(in=in, load=load4, address=address[0..8], out=out4);//4
    RAM512(in=in, load=load5, address=address[0..8], out=out5);//5
    RAM512(in=in, load=load6, address=address[0..8], out=out6);//6
    RAM512(in=in, load=load7, address=address[0..8], out=out7);//7
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, sel=address[9..11], out=out);
}

```

#### Ram16K
```vhdl
/**
 * Memory of 16K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux4Way(in=load, sel=address[12..13], a=load0, b=load1, c=load2, d=load3);
    RAM4K(in=in, load=load0, address=address[0..11], out=out0);//0
    RAM4K(in=in, load=load1, address=address[0..11], out=out1);//1
    RAM4K(in=in, load=load2, address=address[0..11], out=out2);//2
    RAM4K(in=in, load=load3, address=address[0..11], out=out3);//3
    Mux4Way16(a=out0, b=out1, c=out2, d=out3, sel=address[12..13], out=out);
}

```