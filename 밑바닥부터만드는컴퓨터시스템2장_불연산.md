#밑바닥부터만드는컴퓨터시스템/2장_불연산

# Chap02. Gate Implementation Project
## 목표
- 아래의 논리 게이트를 HDL로 구현
	- HalfAdder.hdl
	- FullAdder.hdl
	- Inc16.hdl
	- Add16.hdl
	- ALU.hdl



## 구현 언어

- Hdl ( Hardware Description Language )





## 새롭게 알게 된 점

- nana2tetris의 Chip 파라미터 초기값은 0이다. -> 굳이 0 만들어서 입력하지 않아도 됩니다 ^_^ (당연히 true로 입력하면..)



## 구현
- HalfAdder.hdl
```vhdl
// This file is part of www.nand2tetris.org
// and the book “The Elements of Computing Systems”
// by Nisan and Schocken, MIT Press.
// File name: projects/02/HalfAdder.hdl

/**
 * Computes the sum of two bits.
 */

CHIP HalfAdder {
    IN a, b;    // 1-bit inputs
    OUT sum,    // Right bit of a + b 
        carry;  // Left bit of a + b

    PARTS:
    // Put you code here:
    Xor(a=a,b=b,out=sum);
    And(a=a,b=b,out=carry);
}


```

- FullAdder.hdl
```vhdl

/**
 * Computes the sum of three bits.
 */

CHIP FullAdder {
    IN a, b, c;  // 1-bit inputs
    OUT sum,     // Right bit of a + b + c
        carry;   // Left bit of a + b + c

    PARTS:
    // Put you code here:
    HalfAdder(a=a, b=b, sum=sum1, carry=carry1);
    HalfAdder(a=c, b=sum1, sum=sum, carry=carry2);
    Xor(a=carry1, b=carry2, out=carry);
}

```

- Add16.hdl
```vhdl
/**
 * Adds two 16-bit values.
 * The most significant carry bit is ignored.
 */

CHIP Add16 {
    IN a[16], b[16];
    OUT out[16];

    PARTS: // Put you code here:
    HalfAdder(a=a[0],b=b[0],sum=out[0],carry=c);
    FullAdder(a=a[1],b=b[1],c=c,sum=out[1],carry=d);
    FullAdder(a=a[2],b=b[2],c=d,sum=out[2],carry=e);
    FullAdder(a=a[3],b=b[3],c=e,sum=out[3],carry=f);
    FullAdder(a=a[4],b=b[4],c=f,sum=out[4],carry=g);
    FullAdder(a=a[5],b=b[5],c=g,sum=out[5],carry=h);
    FullAdder(a=a[6],b=b[6],c=h,sum=out[6],carry=i);
    FullAdder(a=a[7],b=b[7],c=i,sum=out[7],carry=j);
    FullAdder(a=a[8],b=b[8],c=j,sum=out[8],carry=k);
    FullAdder(a=a[9],b=b[9],c=k,sum=out[9],carry=l);
    FullAdder(a=a[10],b=b[10],c=l,sum=out[10],carry=m);
    FullAdder(a=a[11],b=b[11],c=m,sum=out[11],carry=n);
    FullAdder(a=a[12],b=b[12],c=n,sum=out[12],carry=o);
    FullAdder(a=a[13],b=b[13],c=o,sum=out[13],carry=p);
    FullAdder(a=a[14],b=b[14],c=p,sum=out[14],carry=q);
    FullAdder(a=a[15],b=b[15],c=q,sum=out[15],carry=drop);
}


```

- Inc16.hdl
```vhdl
/**
 * 16-bit incrementer:
 * out = in + 1 (arithmetic addition)
 */

CHIP Inc16 {
    IN in[16];
    OUT out[16];

    PARTS:
    // Put you code here:
    // 1을 표현하는 방법 ( val Or ! (val) )
    Not(in=in[0], out=notIn);
    Or(a=in[0], b=notIn, out=one);
    HalfAdder(a=in[0],b=one,sum=out[0],carry=carry1);
    HalfAdder(a=in[1],b=carry1,sum=out[1],carry=carry2);
    HalfAdder(a=in[2],b=carry2,sum=out[2],carry=carry3);
    HalfAdder(a=in[3],b=carry3,sum=out[3],carry=carry4);
    HalfAdder(a=in[4],b=carry4,sum=out[4],carry=carry5);
    HalfAdder(a=in[5],b=carry5,sum=out[5],carry=carry6);
    HalfAdder(a=in[6],b=carry6,sum=out[6],carry=carry7);
    HalfAdder(a=in[7],b=carry7,sum=out[7],carry=carry8);
    HalfAdder(a=in[8],b=carry8,sum=out[8],carry=carry9);
    HalfAdder(a=in[9],b=carry9,sum=out[9],carry=carry10);
    HalfAdder(a=in[10],b=carry10,sum=out[10],carry=carry11);
    HalfAdder(a=in[11],b=carry11,sum=out[11],carry=carry12);
    HalfAdder(a=in[12],b=carry12,sum=out[12],carry=carry13);
    HalfAdder(a=in[13],b=carry13,sum=out[13],carry=carry14);
    HalfAdder(a=in[14],b=carry14,sum=out[14],carry=carry15);
    HalfAdder(a=in[15],b=carry15,sum=out[15],carry=dropbit);
}

```

- ALU.hdl

  [nand2tetris/ALU.hdl at master · Olical/nand2tetris · GitHub](https://github.com/Olical/nand2tetris/blob/master/chips/arithmatic/ALU.hdl)
```vhdl
/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2’s complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
   IN
      x[16], y[16],  // 16-bit inputs
      zx, // zero the x input?
      nx, // negate the x input?
      zy, // zero the y input?
      ny, // negate the y input?
      f,  // compute out = x + y (if 1) or x & y (if 0)
      no; // negate the out output?

   OUT
      out[16], // 16-bit output
      zr, // 1 if (out == 0), 0 otherwise
      ng; // 1 if (out < 0),  0 otherwise

   PARTS:
      // x zero / negate
      Mux16(a=x, sel=zx, out=x1);
      Not16(in=x1, out=negatedX);
      Mux16(a=x1, b=negatedX, sel=nx, out=x2);

      // y zero / negate
      Mux16(a=y, sel=zy, out=y1);
      Not16(in=y1, out=negatedY);
      Mux16(a=y1, b=negatedY, sel=ny, out=y2);

      // x + y OR x & y
      Add16(a=x2, b=y2, out=added);
      And16(a=x2, b=y2, out=xyAnd);
      Mux16(a=xyAnd, b=added, sel=f, out=result);

      // negate the output?
      Not16(in=result, out=negatedResult);
      Mux16(a=result, b=negatedResult, sel=no, out=out, out[15]=firstOut, out[0..7]=finalLeft, out[8..15]=finalRight);

      // output == 0 (zr)
      Or8Way(in=finalLeft, out=zrl);
      Or8Way(in=finalRight, out=zrr);
      Or(a=zrl, b=zrr, out=nzr);
      Not(in=nzr, out=zr);

      // output < 0 (ng)
      And(a=firstOut, b=true, out=ng);
}

```
