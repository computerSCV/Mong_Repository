# Chap05. Computer Architecture
#밑바닥부터만드는컴퓨터시스템/5장_컴퓨터아키텍처

1장 - 불 논리
2장 - 불 연산
3장 - 순차 논리
4장 - 기계어
5장 - 1 ~ 4장에서 학습 및 설계한 기계어 및 칩을 통해 Hack 컴퓨터 설계.

즉 5장에서는 앞서 학습한 구성요소를 바탕으로 컴퓨터의 최소 구성 요소(Cpu , Memory , I/O Device)를 갖춘 Hack 컴퓨터를 구현한다.

### 내장식 프로그램
내장식 프로그램이란 이미 결과가 약속된 기본 블록(이미 약속되어 내장된 명령어)의 조합을 바탕으로 무한하게 확장가능한 프로그래밍 가능한 형태를 의미한다. 
 
### 폰 노이만 구조
![](%E1%84%86%E1%85%B5%E1%87%80%E1%84%87%E1%85%A1%E1%84%83%E1%85%A1%E1%86%A8%E1%84%87%E1%85%AE%E1%84%90%E1%85%A5%E1%84%86%E1%85%A1%E1%86%AB%E1%84%83%E1%85%B3%E1%84%82%E1%85%B3%E1%86%AB%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%89%E1%85%B5%E1%84%89%E1%85%B3%E1%84%90%E1%85%A6%E1%86%B75%E1%84%8C%E1%85%A1%E1%86%BC_%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%8B%E1%85%A1%E1%84%8F%E1%85%B5%E1%84%90%E1%85%A6%E1%86%A8%E1%84%8E%E1%85%A5/0BEEF9D9-614E-4F4B-B65C-85898DC3867E.png)

중앙 처리 장치를 중심으로 메모리 , 입출력 장치간 상호 작용을 통해 구현된 컴퓨터 아키텍처를 의미한다.  
메모리 <-  -> 중앙 처리 장치 <- -> 입출력장치 구조를 가진다.

### 메모리
	- 데이터 메모리
	데이터(값)을 저장하는 메모리다.
	- 명령어 메모리
	명령어 ( A-CMD , C-CMD )를 저장하는 메모리다. 명령어 메모리에 저장되어있던 	명령어는 중앙 처리 장치로 인출(FETCH)되어 실행된다.

### 중앙 처리 장치 (CPU)
	- 프로그램의 명령어를 실행하는 역할을 한다. ( 계산 / 값 읽기 및 쓰기 / 조건에 따라 JUMP)
	- 구성요소
	- 산술 논리 연산 장치 (ALU)
		- 저수준 연산(사칙연산) 및 논리연산을 수행하는 장치이다.
	- 레지스터
		- CPU 내부에 존재하는 저장 장치 ( 메모리보다 접근 속도가 빠르다)
	- 제어장치
		- 명령어를 해석하는 장치
		- 다음 실행될 명령어를 지정해주는 역할도 한다.
### 레지스터
	- 메모리에서 데이터를 가져오기위한 절차는 다음과 같다.
	- 메모리 주소 J의 내용을 가져와
		- J를 CPU에서 RAM 으로 전달
		- RAM의 직접 접근 논리에 따라 주소가 J인 메모리 레지스터가 선택
		- RAM[J]의 내용을 CPU로 전달
	- 레지스터에서 가져올 경우 위 과정에서 탐색하는데 소모되는 비용이 없다.

### 입력 및 출력
	- 특정 메모리 주소의 값을 주기적(클록)으로 읽어 입/출력 정보를 감지한다.
	- 이때 특정 메모리 주소를 I/O에 감지용으로 사용하는것을 메모리 매핑이라한다.


- Hack 하드웨어 플랫폼 설계 & 구성도
5장의 프로젝트의 목표는 아래 도식화되어있는 컴퓨터 장치를 설계함으로써 Hack 컴퓨터 플랫폼을 설계하는 것이다.
	- 중앙처리장치(CPU)
![](%E1%84%86%E1%85%B5%E1%87%80%E1%84%87%E1%85%A1%E1%84%83%E1%85%A1%E1%86%A8%E1%84%87%E1%85%AE%E1%84%90%E1%85%A5%E1%84%86%E1%85%A1%E1%86%AB%E1%84%83%E1%85%B3%E1%84%82%E1%85%B3%E1%86%AB%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%89%E1%85%B5%E1%84%89%E1%85%B3%E1%84%90%E1%85%A6%E1%86%B75%E1%84%8C%E1%85%A1%E1%86%BC_%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%8B%E1%85%A1%E1%84%8F%E1%85%B5%E1%84%90%E1%85%A6%E1%86%A8%E1%84%8E%E1%85%A5/EB0762EC-E5CC-450E-996F-C372D217ECEF.png)

	
	- 메모리 모듈 & 메모리 매핑 I/O장치 ( MEMORY )
![](%E1%84%86%E1%85%B5%E1%87%80%E1%84%87%E1%85%A1%E1%84%83%E1%85%A1%E1%86%A8%E1%84%87%E1%85%AE%E1%84%90%E1%85%A5%E1%84%86%E1%85%A1%E1%86%AB%E1%84%83%E1%85%B3%E1%84%82%E1%85%B3%E1%86%AB%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%89%E1%85%B5%E1%84%89%E1%85%B3%E1%84%90%E1%85%A6%E1%86%B75%E1%84%8C%E1%85%A1%E1%86%BC_%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%8B%E1%85%A1%E1%84%8F%E1%85%B5%E1%84%90%E1%85%A6%E1%86%A8%E1%84%8E%E1%85%A5/F9AA82A2-514E-45C6-A393-8F313A4F382B.png)
	

	- Hack 컴퓨터 플랫폼 (COMPUTER )
![](%E1%84%86%E1%85%B5%E1%87%80%E1%84%87%E1%85%A1%E1%84%83%E1%85%A1%E1%86%A8%E1%84%87%E1%85%AE%E1%84%90%E1%85%A5%E1%84%86%E1%85%A1%E1%86%AB%E1%84%83%E1%85%B3%E1%84%82%E1%85%B3%E1%86%AB%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%89%E1%85%B5%E1%84%89%E1%85%B3%E1%84%90%E1%85%A6%E1%86%B75%E1%84%8C%E1%85%A1%E1%86%BC_%E1%84%8F%E1%85%A5%E1%86%B7%E1%84%91%E1%85%B2%E1%84%90%E1%85%A5%E1%84%8B%E1%85%A1%E1%84%8F%E1%85%B5%E1%84%90%E1%85%A6%E1%86%A8%E1%84%8E%E1%85%A5/20E1F2F6-545E-448E-936D-F04EEF5B6781.png)



- Cpu.hdl
- ->  2020 4월 4일 Refactor With Sol
```vhdl

// This file is part of www.nand2tetris.org
// and the book “The Elements of Computing Systems”
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction



    /* 내꺼 */
    PARTS:
    // Put your code here:

    /* Decode */
    And(a=instruction[15], b=true, out=decodeResult); //instruction의 15번째 비트가 output이 됨 -> 0 또는 1(A명령어인지 C명령어인지 구분)
    Not(in=decodeResult, out=decodeResultNot); // A명령어 = 1, C명령어 = 0 으로 뒤집음(FirstMux의 sel비트로 만들기 위함)
    //END

    /* First Mux */
    Mux16(a=ALUoutput, b=instruction, sel=decodeResultNot, out=firstMuxResult);
    //END

    /* A Register */
    Or(a=decodeResultNot, b=instruction[5], out=AregisterControlBit);
    ARegister(in=firstMuxResult, load=AregisterControlBit, out=AregisterResult);
    //A register : decodeResultNot이 1이면(A명령어면) load비트가 1이므로 A레지스터에 주소(0~15비트) 저장, 
    //END

    /* Second Mux */
    And(a=instruction[12], b=decodeResult, out=secondMuxC); 
    // decodeResult가 1이면(C명령어이면) output = instruction[12], decodeResult가 0이면(A명령어이면) output = 0
    Mux16(a=AregisterResult, b=inM, sel=secondMuxC, out=secondMuxResult);
    //END

    /* ALU */
    ALU(x=DregisterResult, y=secondMuxResult, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], 
    out=ALUoutput, zr=zr, ng=ng);
    And(a=instruction[3], b=instruction[15], out=MAndOut);
    Mux16(a=false, b=ALUoutput, sel=MAndOut, out=outM);

    /* D register */
    And(a=instruction[15], b=instruction[4], out=DregisterLoadBit);
    DRegister(in=ALUoutput, load=DregisterLoadBit, out=DregisterResult);

    /* PC */
    And(a=instruction[2], b=ng, out=j1Out); //ng 
    Not(in=ng, out=notNG); //not ng
    Xor(a=notNG, b=zr, out=positive);
    And(a=instruction[1], b=zr, out=j2Out); //zr
    And(a=instruction[0], b=positive, out=j3Out); //positive
    Or(a=j1Out, b=j2Out, out=or1);
    Or(a=or1, b=j3Out, out=or2);
    And(a=instruction[15], b=or2, out=pcLoad);
    PC(in=AregisterResult, load=pcLoad, inc=true, reset=reset, out=pcOut, out[0..14] = pc);
    //END

    /* addressM 으로 보낼 A레지스터의 주소값 */
    And16(a=AregisterResult, b=true, out = andandOUt, out[0..14]=addressM);
    //END

    /* Write M */
    And(a=instruction[3], b=instruction[15], out=writeM, out = writeMClone);
    //END
}

```

- Memory.hdl
```vhdl
// This file is part of www.nand2tetris.org
// and the book “The Elements of Computing Systems”
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer’s memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */


CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    // 00 01 10 11
    PARTS:
    // Put your code here:
    // 1    1   1   1   1   1   1   1   1   1    1     1    1      1       1     sum
    // 1    2   4   8  16  32  64  128 256 512  1024  2048 4096   8192   16384   32768

    DMux4Way(in=load, sel=address[13..14], a=loadRAM1, b=loadRAM2, c=loadSCREEN, d=False);
    Or(a=loadRAM1, b=loadRAM2, out=loadRAM);    
    RAM16K(in=in, load=loadRAM, address=address[0..13], out=outRAM);
    Screen(in=in, load=loadSCREEN, address=address[0..12], out=outSCREEN);
    Keyboard(out=outKEYBOARD);
    Mux4Way16(a=outRAM, b=outRAM, c=outSCREEN, d=outKEYBOARD, sel=address[13..14], out=out);
}

```

- Computer.hdl
```vhdl
// This file is part of www.nand2tetris.org
// and the book “The Elements of Computing Systems”
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Computer.hdl

/**
 * The HACK computer, including CPU, ROM and RAM.
 * When reset is 0, the program stored in the computer’s ROM executes.
 * When reset is 1, the execution of the program restarts. 
 * Thus, to start a program’s execution, reset must be pushed “up” (1)
 * and “down” (0). From this point onward the user is at the mercy of 
 * the software. In particular, depending on the program’s code, the 
 * screen may show some output and the user may be able to interact 
 * with the computer via the keyboard.
 */

/*
—— CPU ——
OUT outM[16],        // M value output
    writeM,          // Write to M? 
    addressM[15],    // Address in data memory (of M)
    pc[15];          // address of next instruction
—— Memory ——
OUT out[16];

—— Rom32K ——
OUT out[16];

*/

CHIP Computer {

    IN reset;
    PARTS:
    // Put your code here:
    ROM32K(address=pc, out=instruction);
    CPU(inM=inM, instruction=instruction, reset=reset, outM=outM, writeM=writeM, addressM=addressM, pc=pc);
    Memory(in=outM, load=writeM, address=addressM, out=inM);
    
    // 컴퓨터란
    // 1.메모리로부터 값을 읽어 CPU에 전달하고
    // 2.CPU는 약속된 명령어를 실행시켜 메모리에 전달한다.
    // 3. 1 - 2 를 계속해서 반복하는것이 컴퓨터이다.

}


```


